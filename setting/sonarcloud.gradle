//sonarCloud
sonar {
    properties {
        property "sonar.projectKey", "Team-INSERT_BUMAWIKI_SERVER_V2"
        property "sonar.organization", "team-insert"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.java.checkstyle.reportPaths", "build/reports/checkstyle/main.xml"
    }
}

System.setProperty("sonar.gradle.skipCompile", "true")
System.setProperty("sonar.skipCompile", "true")

jacoco {
    toolVersion = '0.8.12'
}

//sonarCloud testCoverage
jacocoTestReport {
    dependsOn test
    reports {
        html.required = true
        xml.required = true
    }

    def QDomains = []

    for (qPattern in '**/QA'..'**/QZ') {
        QDomains.add(qPattern + '*')
    }

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, excludes: ["com.project.bumawiki.BumawikiApplication",
                                         "**.*Config*",
                                         "**.*Exception*",
                                         "**.*Dto*"] + QDomains)
        }))
    }

    finalizedBy jacocoTestCoverageVerification
}

tasks.named('test') {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}